# Maintainer: Stanislaw Grams <sjg@fmdx.pl>

pkgname=libairspyhf-git
pkgver=20190726
pkgrel=1
pkgdesc="User mode driver for Airspy HF+"
arch=('x86_64')
url="http://www.airspy.com"
license=('GPL2')
depends=('libusb>=1.0')
provides=('libairspyhf')
conflicts=('libairspyhf')
makedepends=('git' 'cmake' 'pkgconf')
source=("git+https://github.com/airspy/airspyhf")
md5sums=('SKIP')
_gitname="airspyhf"

pkgver() {
  cd "$srcdir/$_gitname"
  git show -s --format="%ci" HEAD | sed -e 's/-//g' -e 's/ .*//'
}

build() {
  cd "$srcdir/$_gitname"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -Wno-dev -DINSTALL_UDEV_RULES=ON ../
  make
}

package() {
  cd "$srcdir/$_gitname/build"
  make DESTDIR="$pkgdir" install
}
