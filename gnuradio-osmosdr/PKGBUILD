# Maintainer: Kyle Keen <keenerd@gmail.com?>
# Contributor: Dominik Heidler <dheidler@gmail.com>
# Contributor: Stanis≈Çaw Grams <sjg@fmdx.pl>

pkgname=gnuradio-osmosdr
_pkgname=gr-osmosdr
_r=128
_commit=7da9989b7a803372806214cc1e49690a848fed26
pkgver=0.1.5.r$_r.${_commit:0:7}
pkgrel=3
pkgdesc='Source block for Funcube Dongle, RTL-SDR, USRP, OsmoSDR, BladeRF, HackRF and AirSpy devices'
url='https://sdr.osmocom.org/trac/'
arch=('x86_64')
license=('GPL')
depends=('gnuradio' 'gnuradio-iqbal' 'rtl-sdr' 'gnuradio-fcdproplus' 'hackrf' 'bladerf' 'airspy' 'soapysdr')
optdepends=('python-opengl: osmocom_fft')
makedepends=('git' 'cmake' 'boost' 'swig')
replaces=('gr-osmosdr')
source=("git://github.com/fmdxpl/gr-osmosdr/#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cd $_pkgname
}

build() {
  cd $_pkgname
  mkdir -p build && cd build
  cmake \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE="$(which python3)" \
    -DPYTHON_INCLUDE_DIR="$(echo /usr/include/python3*)" \
    -DPYTHON_LIBRARY="$(echo /usr/lib/libpython3.*.so)" \
    -DBoost_NO_BOOST_CMAKE=ON \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make -j8
}

package() {
  cd $_pkgname/build
  make DESTDIR="$pkgdir" install
}

